global EntryLoop: int = 0
global ElCascade: int = 0

void ruby_tok_list( TL: host::tok* )
{
	for Tok: host::tok in repeat(TL) {
		if match Tok [host::`${ StmtList: stmt* host::`}$]
			ruby_stmt_list( StmtList )
		elsif match Tok [host::`={ Expr: expr host::`}=]
			ruby_expr( Expr )
		else {
			send Output [Tok]
		}
	}
}

void ruby_expr_factor( ExprFactor: expr_factor )
{
	if match ExprFactor
		[`@ string uint `{ TL: host::tok* host::`}$]
	{
		ruby_tok_list( TL )
	}
	elsif match ExprFactor
		[`$ string uint `{ TL: host::tok* host::`}$]
	{
		send Output
			"begin
			"[ruby_tok_list( TL )]
			"end
	}
	elsif match ExprFactor
		[`= string uint `{ TL: host::tok* host::`}$]
	{
		ruby_tok_list( TL )
	}
	elsif match ExprFactor
		[O:`( TL: expr C: `)]
	{
		send Output
			[O ruby_expr(TL) C]
	}
	elsif match ExprFactor
		[ident O: `[ TL: expr C: `]]
	{
		send Output
			[ExprFactor.ident O ruby_expr( TL ) C]
	}
	elsif match ExprFactor
		['offset' '(' ident ',' expr ')']
	{
		send Output
			[ruby_expr( ExprFactor.expr )]
	}
	elsif match ExprFactor
		['deref' '(' ident ',' expr ')']
	{
		send Output
			[ ExprFactor.ident '[' ruby_expr( ExprFactor.expr ) ']']
		if $ExprFactor.ident == 'data'
			send Output '.ord'
	}
	elsif match ExprFactor
			[T: `TRUE]
	{
		T.data = 'true'
		send Output [T]
	}
	elsif match ExprFactor
			[F: `FALSE]
	{
		F.data = 'false'
		send Output [F]
	}
	elsif match ExprFactor
			[N: `nil]
	{
		N.data = '0'
		send Output [N]
	}
	elsif match ExprFactor
			[Number: number]
	{
		ruby_number( Number )
	}
	else {
		# Catches cases not specified
		send Output [ExprFactor]
	}
}

void ruby_expr_ptr( ExprPtr: expr_ptr )
{
	if match ExprPtr [expr_ptr Arrow: `-> expr_factor]
	{
		ruby_expr_ptr( ExprPtr._expr_ptr )
		send Output [Arrow]
		ruby_expr_factor( ExprPtr.expr_factor )
	}
	elsif match ExprPtr [Star: `* expr_ptr]
	{
		send Output [Star]
		ruby_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [PP: `++ expr_ptr]
	{
		send Output [PP]
		ruby_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [MM: `-- expr_ptr]
	{
		send Output [MM]
		ruby_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [expr_ptr PP: `++]
	{
		ruby_expr_ptr( ExprPtr._expr_ptr )
		send Output [PP]
	}
	elsif match ExprPtr [expr_ptr MM: `--]
	{
		ruby_expr_ptr( ExprPtr._expr_ptr )
		send Output [MM]
	}
	elsif match ExprPtr [Open: `( Type: type Close: `) expr_ptr]
	{
		#send Output [Open]
		#ruby_type( Type )
		#send Output [Close]
		ruby_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [expr_factor]
	{
		ruby_expr_factor( ExprPtr.expr_factor )
	}
	else {
		send Output [ExprPtr]
	}
}

void ruby_expr_factor_op( ExprFactorOp: expr_factor_op )
{
	if match ExprFactorOp [B: `! expr_factor_op]
	{
		send Output [B]
		ruby_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [T: `~ expr_factor_op]
	{
		send Output [T]
		ruby_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [expr_ptr]
	{
		ruby_expr_ptr( ExprFactorOp.expr_ptr )
	}
}

void ruby_expr_bitwise( ExprBitwise: expr_bitwise )
{
	if match ExprBitwise [expr_bitwise A: `& expr_factor_op]
	{
		ruby_expr_bitwise( ExprBitwise._expr_bitwise )
		send Output [A]
		ruby_expr_factor_op( ExprBitwise.expr_factor_op )
	}
	elsif match ExprBitwise [expr_factor_op]
	{
		ruby_expr_factor_op( ExprBitwise.expr_factor_op )
	}
}

void ruby_expr_mult( ExprMult: expr_mult )
{
	if match ExprMult [expr_mult T: `* expr_bitwise]
	{
		ruby_expr_mult( ExprMult._expr_mult )
		send Output [T]
		ruby_expr_bitwise( ExprMult.expr_bitwise )
	}
	elsif match ExprMult [expr_bitwise]
	{
		ruby_expr_bitwise( ExprMult.expr_bitwise )
	}
}

void ruby_expr_add( ExprAdd: expr_add )
{
	if match ExprAdd [expr_add Op: add_op expr_mult]
	{
		ruby_expr_add( ExprAdd._expr_add )
		send Output [Op]
		ruby_expr_mult( ExprAdd.expr_mult )
	}
	elsif match ExprAdd [expr_mult]
	{
		ruby_expr_mult( ExprAdd.expr_mult )
	}
}

void ruby_expr_shift( ExprShift: expr_shift )
{
	if match ExprShift [expr_shift Op: shift_op expr_add]
	{
		ruby_expr_shift( ExprShift._expr_shift )
		send Output [Op]
		ruby_expr_add( ExprShift.expr_add )
	}
	elsif match ExprShift [expr_add]
	{
		ruby_expr_add( ExprShift.expr_add )
	}
}

void ruby_expr_test( ExprTest: expr_test )
{
	if match ExprTest [expr_test Op: test_op expr_shift]
	{
		ruby_expr_test( ExprTest._expr_test )
		send Output [Op]
		ruby_expr_shift( ExprTest.expr_shift )
	}
	elsif match ExprTest [expr_shift]
	{
		ruby_expr_shift( ExprTest.expr_shift )
	}
}

void ruby_expr_cond( ExprCond: expr_cond )
{
	ruby_expr_test( ExprCond.expr_test )
}

void ruby_expr_assign( ExprAssign: expr_assign )
{
	if match ExprAssign [expr_assign Op: assign_op expr_cond]
	{
		ruby_expr_assign( ExprAssign._expr_assign )
		send Output [Op]
		ruby_expr_cond( ExprAssign.expr_cond )
	}
	elsif match ExprAssign [expr_cond]
	{
		ruby_expr_cond( ExprAssign.expr_cond )
	}
}

void ruby_expr( Expr: expr )
{
	ruby_expr_assign( Expr.expr_assign )
}

void ruby_type( Type: type )
{
	if match Type "s8"
		send Output ['byte ']
	elsif match Type "s16"
		send Output ['short ']
	elsif match Type "s32"
		send Output ['int ']
	elsif match Type "s64"
		send Output ['long ']
	elsif match Type "s128"
		send Output ['long long ']
	elsif match Type "uint"
		send Output ['int ']
	else
		send Output [Type]
}

void ruby_number( Number: number )
{
	if match Number [`u `( uint `) ]
		send Output "[Number.uint]"
	else
		send Output [Number]
}

void ruby_num_list( NumList: num_list )
{
	for Number: number in NumList
		send Output "[ruby_number( Number )], "
}

void ruby_stmt( Stmt: stmt )
{
	if match Stmt
		[`@ string uint `{ TL: host::tok* host::`}$]
	{
		ruby_tok_list( TL )
	}
	elsif match Stmt
		[`$ string uint `{ TL: host::tok* host::`}$]
	{
		send Output
			"begin
			"[ruby_tok_list( TL )]
			"end
	}
	elsif match Stmt
		[`= string uint `{ TL: host::tok* host::`}$]
	{
		ruby_tok_list( TL )
	}
	elsif match Stmt [Array: array] {
		send Output
			"class << self
			"	attr_accessor :[ Array.ident ] 
			"	private :[ Array.ident ], :[ Array.ident ]=
			"end
			"self.[ Array.ident ] = \[
			"	[ruby_num_list( Array.num_list )]
			"\]
			"
	}
	elsif match Stmt [Value: value] {
		send Output
			"class << self
			"	attr_accessor :[ Value.value_pass.ident ]
			"end
			"self.[ Value.value_pass.ident ] = [ Value.value_pass.number ];
			"
	}
	elsif match Stmt [
			'if' O: `( IfExpr: expr C: `) IfStmt: stmt
			ElseIfClauseList: else_if_clause* ElseClauseOpt: else_clause?
	] {
		send Output
			"if ( [ruby_expr(IfExpr)] )
			"	[ruby_stmt(IfStmt)]

		for ElseIfClause: else_if_clause in repeat( ElseIfClauseList ) {
			match ElseIfClause
				['else if (' ElseIfExpr: expr ')' ElseIfStmt: stmt]

			send Output
				"elsif ( [ruby_expr(ElseIfExpr)] )
				"	[ruby_stmt(ElseIfStmt)]
		}

		if ( match ElseClauseOpt ['else' ElseStmt: stmt] ) {
			send Output
				"else
				"	[ruby_stmt(ElseStmt)]
		}
		send Output
			"end
	}
	elsif match Stmt ['while' '(' WhileExpr: expr ')' WhileStmt: stmt] {
		ElCascade = ElCascade + 1
		send Output
			"while ( [ruby_expr(WhileExpr)] )
			"	[ruby_stmt(WhileStmt)]
			"end
		if EntryLoop {
			send Output
				"next if _trigger_goto
		}
		ElCascade = ElCascade - 1
	}
	elsif match Stmt ['switch' '(' SwitchExpr: expr ')' '{' StmtList: stmt* '}'] {
		send Output
			"case  [ruby_expr(SwitchExpr)] 
			"when -2 then
			"begin
			"	[ruby_stmt_list(StmtList)]
			"end
			"end
	}
	elsif match Stmt [ExprExpr: expr Semi: `;] {
		send Output
			[ruby_expr(ExprExpr) Semi]
	}
	elsif match Stmt [L: `{ TL: stmt* R: `}] {
		send Output
			"begin
			"[ruby_stmt_list(TL)]
			"end
	}
	elsif match Stmt [TypeList: opt_const Type: type OptPtr: opt_ptr Ident: ident OptInit: opt_init Semi: `;]
	{
		send Output
			[OptPtr Ident]

		if match OptInit [E: `= expr] {
			send Output
				[E ruby_expr(OptInit.expr)]
		}
		else {
			send Output
				"= 0
		}

		send Output
			[Semi]
	}
	elsif match Stmt [Export: export_stmt]
	{
		send Output
			"class << self
			"	attr_accessor :[ Export.ident ]
			"end
			"self.[ Export.ident ] = [ ruby_number(Export.number) ];
			"
	}
	elsif match Stmt ['fallthrough' ';']
	{
		# Nothing needed here.
	}
	elsif match Stmt [Index: index_stmt]
	{
		send Output
			"[Index.ident] = 0;
	}
	elsif match Stmt [case_label]
	{
		send Output
			"end
			"when [ruby_expr( Stmt.case_label.expr )] then
			"begin
			"[ruby_stmt_list( Stmt.case_label._repeat_stmt )]
	}
	elsif match Stmt [goto_label]
	{
		send Output "{}"
	}
	elsif match Stmt [G: goto_stmt]
	{
		if ( ElCascade ) {
			send Output
				"begin
				"	_trigger_goto = true
				"	_goto_level = [$G.Id]
				"	break
				"end
		}
		else {
			send Output
				"begin
				"	_goto_level = [$G.Id]
				"	next
				"end
		}
	}
	elsif match Stmt [entry_loop]
	{
		EntryLoop = EntryLoop + 1
		send Output
			"_goto_level = 0
			"while true 
			"	_trigger_goto = false
			"	if _goto_level <= 0 
			"	[ruby_stmt_list( Stmt.entry_loop._repeat_stmt )]
			"	end
			"	break
			"end
		EntryLoop = EntryLoop - 1
	}
	elsif match Stmt [label_stmt]
	{
		send Output
			"end
			"if _goto_level <= [Stmt.label_stmt.Id]
			"[ruby_stmt_list( Stmt.label_stmt._repeat_stmt )]
	}
	else {
		# catches unspecified cases
		send Output [Stmt]
	}
}

void ruby_stmt_list( StmtList: stmt* )
{
	for Stmt: stmt in repeat( StmtList )
		ruby_stmt( Stmt )
}
