void d_tok_list( TL: host::tok* )
{
	for Tok: host::tok in repeat(TL) {
		if match Tok [host::`${ StmtList: stmt* host::`}$]
			d_stmt_list( StmtList )
		elsif match Tok [host::`={ Expr: expr host::`}=]
			d_expr( Expr )
		else {
			send Output [Tok]
		}
	}
}

void d_expr_factor( ExprFactor: expr_factor )
{
	if match ExprFactor
		[`@ string uint `{ TL: host::tok* host::`}$]
	{
		d_tok_list( TL )
	}
	elsif match ExprFactor
		[`$ string uint `{ TL: host::tok* host::`}$]
	{
		send Output "{[d_tok_list( TL )]}
	}
	elsif match ExprFactor
		[`= string uint `{ TL: host::tok* host::`}$]
	{
		d_tok_list( TL )
	}
	elsif match ExprFactor
		[O:`( TL: expr C: `)]
	{
		send Output
			[O d_expr( TL ) C]
	}
	elsif match ExprFactor
		[ident O: `[ TL: expr C: `]]
	{
		send Output
			[ExprFactor.ident O d_expr(TL) C]
	}
	elsif match ExprFactor
		[`offset `( ident `, expr `)]
	{
		send Output
			"& [ExprFactor.ident] \[ [d_expr(ExprFactor.expr)] \]
	}
	elsif match ExprFactor
		[`deref `( ident `, expr `)]
	{
		send Output
			"(*( [d_expr(ExprFactor.expr)] ))
	}
	elsif match ExprFactor
			[T: `TRUE]
	{
		T.data = '1'
		send Output [T]
	}
	elsif match ExprFactor
			[F: `FALSE]
	{
		F.data = '0'
		send Output [F]
	}
	elsif match ExprFactor
			[N: `nil]
	{
		N.data = 'null'
		send Output [N]
	}
	elsif match ExprFactor
			[Number: number]
	{
		d_number( Number )
	}
	else {
		# Catches cases not specified
		send Output [ExprFactor]
	}
}

void d_expr_ptr( ExprPtr: expr_ptr )
{
	if match ExprPtr [expr_ptr Arrow: `-> expr_factor]
	{
		d_expr_ptr( ExprPtr._expr_ptr )
		send Output [Arrow]
		d_expr_factor( ExprPtr.expr_factor )
	}
	elsif match ExprPtr [Star: `* expr_ptr]
	{
		send Output [Star]
		d_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [PP: `++ expr_ptr]
	{
		send Output [PP]
		d_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [MM: `-- expr_ptr]
	{
		send Output [MM]
		d_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [expr_ptr PP: `++]
	{
		d_expr_ptr( ExprPtr._expr_ptr )
		send Output [PP]
	}
	elsif match ExprPtr [expr_ptr MM: `--]
	{
		d_expr_ptr( ExprPtr._expr_ptr )
		send Output [MM]
	}
	elsif match ExprPtr [Open: `( Type: type Close: `) expr_ptr]
	{
		send Output ['cast' Open]
		d_type( Type )
		send Output [Close]
		d_expr_ptr( ExprPtr._expr_ptr )
	}
	elsif match ExprPtr [expr_factor]
	{
		d_expr_factor( ExprPtr.expr_factor )
	}
	else {
		send Output [ExprPtr]
	}
}

void d_expr_factor_op( ExprFactorOp: expr_factor_op )
{
	if match ExprFactorOp [B: `! expr_factor_op]
	{
		send Output [B]
		d_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [T: `~ expr_factor_op]
	{
		send Output [T]
		d_expr_factor_op( ExprFactorOp._expr_factor_op )
	}
	elsif match ExprFactorOp [expr_ptr]
	{
		d_expr_ptr( ExprFactorOp.expr_ptr )
	}
}

void d_expr_bitwise( ExprBitwise: expr_bitwise )
{
	if match ExprBitwise [expr_bitwise A: `& expr_factor_op]
	{
		d_expr_bitwise( ExprBitwise._expr_bitwise )
		send Output [A]
		d_expr_factor_op( ExprBitwise.expr_factor_op )
	}
	elsif match ExprBitwise [expr_factor_op]
	{
		d_expr_factor_op( ExprBitwise.expr_factor_op )
	}
}

void d_expr_mult( ExprMult: expr_mult )
{
	if match ExprMult [expr_mult T: `* expr_bitwise]
	{
		d_expr_mult( ExprMult._expr_mult )
		send Output [T]
		d_expr_bitwise( ExprMult.expr_bitwise )
	}
	elsif match ExprMult [expr_bitwise]
	{
		d_expr_bitwise( ExprMult.expr_bitwise )
	}
}

void d_expr_add( ExprAdd: expr_add )
{
	if match ExprAdd [expr_add Op: add_op expr_mult]
	{
		d_expr_add( ExprAdd._expr_add )
		send Output [Op]
		d_expr_mult( ExprAdd.expr_mult )
	}
	elsif match ExprAdd [expr_mult]
	{
		d_expr_mult( ExprAdd.expr_mult )
	}
}

void d_expr_shift( ExprShift: expr_shift )
{
	if match ExprShift [expr_shift Op: shift_op expr_add]
	{
		d_expr_shift( ExprShift._expr_shift )
		send Output [Op]
		d_expr_add( ExprShift.expr_add )
	}
	elsif match ExprShift [expr_add]
	{
		d_expr_add( ExprShift.expr_add )
	}
}

void d_expr_test( ExprTest: expr_test )
{
	if match ExprTest [expr_test Op: test_op expr_shift]
	{
		d_expr_test( ExprTest._expr_test )
		send Output [Op]
		d_expr_shift( ExprTest.expr_shift )
	}
	elsif match ExprTest [expr_shift]
	{
		d_expr_shift( ExprTest.expr_shift )
	}
}

void d_expr_cond( ExprCond: expr_cond )
{
	d_expr_test( ExprCond.expr_test )
}

void d_expr_assign( ExprAssign: expr_assign )
{
	if match ExprAssign [expr_assign Op: assign_op expr_cond]
	{
		d_expr_assign( ExprAssign._expr_assign )
		send Output [Op]
		d_expr_cond( ExprAssign.expr_cond )
	}
	elsif match ExprAssign [expr_cond]
	{
		d_expr_cond( ExprAssign.expr_cond )
	}
}

void d_expr( Expr: expr )
{
	d_expr_assign( Expr.expr_assign )
}

void d_type( Type: type )
{
	if match Type "s8"
		send Output ['byte ']
	elsif match Type "s16"
		send Output ['short ']
	elsif match Type "s32"
		send Output ['int ']
	elsif match Type "s64"
		send Output ['long ']
	elsif match Type "s128"
		send Output ['long long ']
	else
		send Output [Type]
}

void d_number( Number: number )
{
	if match Number [`u `( uint `) ]
		send Output "[Number.uint]u"
	else
		send Output [Number]
}

void d_num_list( NumList: num_list )
{
	for Number: number in NumList
		send Output "[d_number( Number )], "
}

void d_stmt( Stmt: stmt )
{
	if match Stmt
		[`@ string uint `{ TL: host::tok* host::`}$]
	{
		d_tok_list( TL )
	}
	elsif match Stmt
		[`$ string uint `{ TL: host::tok* host::`}$]
	{
		send Output "{[d_tok_list( TL )]}
	}
	elsif match Stmt
		[`= string uint `{ TL: host::tok* host::`}$]
	{
		d_tok_list( TL )
	}
	elsif match Stmt [Array: array] {
		send Output
			"static const [d_type(Array.type)] "
				"[Array.ident] \[\] = \[ [d_num_list( Array.num_list )] \];
	}
	elsif match Stmt [Value: value] {
		send Output
			"static const [Value.value_pass]
	}
	elsif match Stmt [
			`if `( IfExpr: expr `) IfStmt: stmt
			ElseIfClauseList: else_if_clause* ElseClauseOpt: else_clause?
	] {
		send Output
			"if ( [d_expr(IfExpr)] )
			"	[d_stmt(IfStmt)]

		for ElseIfClause: else_if_clause in repeat( ElseIfClauseList ) {
			match ElseIfClause
				[`else `if `( ElseIfExpr: expr `) ElseIfStmt: stmt]

			send Output
				"else if ( [d_expr(ElseIfExpr)] )
				"	[d_stmt(ElseIfStmt)]
		}

		if ( match ElseClauseOpt ['else' ElseStmt: stmt] ) {
			send Output
				"else 
				"	[d_stmt(ElseStmt)]
		}
	}
	elsif match Stmt [`while `( WhileExpr: expr `) WhileStmt: stmt] {
		send Output
			"while ( [d_expr(WhileExpr)] )
			"	[d_stmt(WhileStmt)]
	}
	elsif match Stmt [`switch `( SwitchExpr: expr `) `{ StmtList: stmt* `}] {
		send Output
			"switch ( [d_expr(SwitchExpr)] ) {
			"	[d_stmt_list(StmtList)]
			"	default: break;
			"}
	}
	elsif match Stmt [ExprExpr: expr Semi: `;] {
		send Output
			[d_expr( ExprExpr ) Semi]
	}
	elsif match Stmt [L: `{ TL: stmt* R: `}] {
		send Output
			[L d_stmt_list( TL ) R]
	}
	elsif match Stmt [OptConst: opt_const Type: type OptPtr: opt_ptr Ident: ident OptInit: opt_init Semi: `;]
	{
		if match OptConst ['const'] {
			send Output
				"const( [d_type( Type )] )
		}
		else {
			d_type( Type )
		}

		send Output [OptPtr Ident]

		if match OptInit [E: `= expr] {
			send Output
				[E d_expr(OptInit.expr)]
		}

		send Output [Semi]
	}
	elsif match Stmt [case_label]
	{
		send Output
			"case [d_expr( Stmt.case_label.expr )]:
	}
	elsif match Stmt [export_stmt]
	{
		send Output
			"static const [d_type(Stmt.export_stmt.type)] "
					"[Stmt.export_stmt.ident] = [d_number(Stmt.export_stmt.number)];
	}
	elsif match Stmt ['fallthrough' ';']
	{
		send Output "goto case;"
	}
	elsif match Stmt [index_stmt]
	{
		send Output
			"const([d_type(Stmt.index_stmt.type)]) *[Stmt.index_stmt.ident];
	}
	elsif match Stmt [label_stmt]
	{
		send Output
			"[Stmt.label_stmt.ident]:
			"[d_stmt_list( Stmt.label_stmt._repeat_stmt )]
	}
	elsif match Stmt [entry_loop]
	{
		send Output
			[d_stmt_list( Stmt.entry_loop._repeat_stmt )]
	}
	else {
		# catches unspecified cases
		send Output [Stmt]
	}
}

void d_stmt_list( StmtList: stmt* )
{
	for Stmt: stmt in repeat( StmtList )
		d_stmt( Stmt )
}


